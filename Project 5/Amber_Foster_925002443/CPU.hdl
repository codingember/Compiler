// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction, b=ALUout, sel=instruction[15], out=ARin);
    Not(in=instruction[15], out=ninstruction);

    Or(a=ninstruction, b=instruction[5], out=loadAR);
    ARegister(in=ARin, load=loadAR, out=ARout, out[0..14]=addressM);
    Mux16(a=ARout, b=inM, sel=instruction[12], out=ARMout);
    And(a=instruction[11], b=instruction[15], out=zx);
    And(a=instruction[10], b=instruction[15], out=nx);
    Or(a=instruction[9], b=ninstruction, out=zy);
    Or(a=instruction[8], b=ninstruction, out=ny);
    And(a=instruction[7], b=instruction[15], out=f);
    And(a=instruction[6], b=instruction[15], out=no);
    ALU(x=DRout, y=ARMout, zx=zx, nx=nx, zy=zy, ny=ny, f=f, no=no, out=outM, out=ALUout, zr=zero, ng=neg);

    And(a=instruction[15], b=instruction[3], out=writeM);
    And(a=instruction[15], b=instruction[4], out=loadDR);
    DRegister(in=ALUout, load=loadDR, out=DRout);

    Or(a=zero, b=neg, out=npos);
    Not(in=npos, out=pos);

    And(a=instruction[0], b=pos, out=jump2);
    And(a=instruction[1], b=zero, out=jump1);
    And(a=instruction[2], b=neg, out=jump0);
    Or(a=jump0, b=jump1, out=jump3);
    Or(a=jump3, b=jump2, out=jump4);
    And(a=jump4, b=instruction[15], out=jump);

    PC(in=ARout, load=jump, reset=reset, inc=true, out[0..14]=pc);
}